#!/usr/bin/env bb
;; vim: set ft=clojure:

(require '[babashka.process :refer [shell exec process]]
         '[babashka.fs :as fs]
         '[clojure.edn :refer [read-string]]
         '[clojure.string :as str])

;; Переменные, которые используются для подстановки.
(def ^:dynamic filename nil)

;; Путь к файлу с настройками.
(def associations-file (str (System/getenv "HOME") "/.config/associations.edn"))

(defn gui?
  "Функция для определения, запускается приложение из консоли или нет."
  []
  (nil? (System/console)))

(defn mime
  "Функция для определения mime типа."
  [name]
  (when (fs/exists? name)
    (-> (shell {:out :string} "mimetype" "--brief" "--dereference" name)
        :out
        str/trim-newline)))

(defn file
  "Функция возвращает имя файла, если файл существует."
  [name]
  (when (fs/exists? name)
    name))

(defn schm
  "Функция проверяет, является ли переданный аргумент схемой."
  [name]
  (when (re-matches #"^[a-z]+://.*" name)
    name))

(defn check-pattern
  "Проверяет указанную сущность по паттерну. Оба аргумента не должны быть пустыми."
  [pattern entity]
  (and pattern entity (re-matches (re-pattern pattern) entity)))

(defn association-matches
  "Проверяет, подходит ли ассоциация под mime тип и соответствует ли ассоциация
  способу запуска (из графического интерфейса или из терминала)."
  [call-from-gui filename mimetype scheme association]
  (and (or (check-pattern (:file   association) filename)
           (check-pattern (:mime   association) mimetype)
           (check-pattern (:scheme association) scheme))
       (or (nil? (:gui association))
           (= (:gui association) call-from-gui))))

(defn get-association
  "Находит ассоциацию, соответствующую mime типу и способу запуска (из gui или
  из терминала)."
  [associations argument]
  (let [filename  (file argument)
        mimetype  (mime argument)
        scheme    (schm argument)
        call-from-gui (gui?)]
    (first
      (drop-while
        (complement (partial association-matches call-from-gui filename mimetype scheme))
        associations))))

(let [filename-arg (first *command-line-args*)
      associations (read-string (slurp associations-file))
      selected-ass (get-association associations filename-arg)]
  (when selected-ass
    (binding [filename filename-arg]
      ((if (get selected-ass :detach false) process exec)
       (eval (:prog selected-ass))))))
